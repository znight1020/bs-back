plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version '3.4.5'
    id 'jacoco'
}

group = 'toy'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // WEB
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // PERSIST
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.mysql:mysql-connector-j'
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    runtimeOnly 'com.h2database:h2'

    // AWS
    implementation 'io.awspring.cloud:spring-cloud-aws-starter:3.3.0'
    implementation 'software.amazon.awssdk:s3:2.31.38'

    // SMTP
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // GEO
    implementation 'org.hibernate.orm:hibernate-spatial'
    implementation 'org.locationtech.jts:jts-core:1.18.2'

    // TEST
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // TEST CONTAINER
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testImplementation 'org.testcontainers:mysql:1.19.3'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport', 'jacocoTestCoverageVerification'
}

configurations {
    unitTestImplementation.extendsFrom testImplementation, implementation
    unitTestAnnotationProcessor.extendsFrom annotationProcessor
    integrationTestImplementation.extendsFrom testImplementation, implementation
    integrationTestAnnotationProcessor.extendsFrom annotationProcessor
}

def envFile = rootProject.file(".env")
if (envFile.exists()) {
    envFile.readLines()
        .findAll { it && it.contains("=") }
        .each { line ->
            def (key, value) = line.split("=")
            if (key == "USERNAME") {
                System.setProperty("jib.from.auth.username", value)
                System.setProperty("jib.to.auth.username", value)
            }
            if (key == "PASSWORD") {
                System.setProperty("jib.from.auth.password", value)
                System.setProperty("jib.to.auth.password", value)
            }
        }
}

jib {
    from {
        // image = 'container-registry.oracle.com/java/openjdk:17'
        image = 'eclipse-temurin:17-jre'
        auth {
            username = System.getProperty("jib.from.auth.username")
            password = System.getProperty("jib.from.auth.password")
        }
    }

    to {
        image = 'leehyeonsoo/bserver'
        tags = [
                // 운영, 개발 API 서버 태그 분기
                project.findProperty("jibProfile") == "dev"
                        ? "bserver-dev"
                        : "bserver-prod"
        ]

        // DockerHub 인증
        def user = System.getProperty("jib.to.auth.username")
        def pass = System.getProperty("jib.to.auth.password")
        if (user != null && pass != null) {
            auth {
                username = user
                password = pass
            }
        }
    }

    container {
        ports = ['8080']
        jvmFlags = [
                "-Dspring.profiles.active=" +
                        (project.findProperty("jibProfile") == "dev"
                                ? "dev,secret"
                                : "prod,secret")
        ]
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
}

sourceSets {
    test {
        java.srcDirs = ['src/test/unit/java']
        resources {
            srcDirs = ['src/test/unit/resources']
        }
    }

    unitTest {
        java.srcDirs = ['src/test/unit/java']
        resources {
            srcDir 'src/test/unit/resources'
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }

    integrationTest {
        java.srcDirs = ['src/test/intg/java']
        resources {
            srcDir 'src/test/intg/resources'
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

tasks.register('unitTest', Test) {
    description = "Runs unit tests"
    group = "verification"
    testClassesDirs = sourceSets.unitTest.output.classesDirs
    classpath = sourceSets.unitTest.runtimeClasspath
    useJUnitPlatform()
}
unitTest.finalizedBy(clean)

tasks.register('integrationTest', Test) {
    description = "Runs integration tests"
    group = "verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
}
integrationTest.finalizedBy(clean)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'

            excludes = [
                    'com.bob.BookBridgeApplication',
                    'com.bob.global.audit.BaseTime',
                    'com.bob.**.config.**',
                    'com.bob.**.request.**',
                    'com.bob.**.response.**',
                    'com.bob.**.command.**',
                    'com.bob.**.query.**',
                    'com.bob.**.symbol.**',
                    'com.bob.**.dsl.**', // QueryDSL Impl 제외, 도메인 통합 테스트에서 검증
                    'com.bob.**.Q*', // QueryDSL Q 클래스 제외
            ]

            // LINE 커버리지
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            // BRANCH 커버리지
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required.set(true)
        //xml.required.set(false)
        //csv.required.set(false)
    }
    classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        'com.bob.global.audit.BaseTime',
                        'com.bob.BookBridgeApplication.class'
                ])
            })
    )
}
